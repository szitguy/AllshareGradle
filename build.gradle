plugins {
    id "com.jfrog.bintray" version "1.7"
    id 'de.fuerstenau.buildconfig' version '1.1.8'
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'maven'

//def bintrayApiKey = BINTRAY_API_KEY
//def bintrayUser = BINTRAY_USER
def bintrayApiKey = 'BINTRAY_API_KEY'
def bintrayUser = 'BINTRAY_USER'
def group_id = 'com.github.szitguy'
def artifact_id = 'allshare-gradle-plugin'
def versionName = '1.0.7-3'
//def debug = true
def debug = false
def androidLibVersion = '1.0.5'

group group_id
version versionName

buildConfig {
    buildConfigField 'boolean', 'DEBUG', "${debug}"
    buildConfigField 'String', 'ANDROID_LIB_VERSION', "${androidLibVersion}"
}

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile group: 'com.googlecode.jarjar', name: 'jarjar', version: '1.3'

    compile 'com.android.tools.build:gradle:2.3.1'
}

// ---------- 上传bintray时用以下脚本 ----------

bintray {
    user = bintrayUser
    key = bintrayApiKey
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = artifact_id
        desc = 'a gradle plugin for allshare to modify some code'
        websiteUrl = 'https://github.com/szitguy/AllshareGradle'
        issueTrackerUrl = 'https://github.com/szitguy/AllshareGradle/issues'
        vcsUrl = 'https://github.com/szitguy/AllshareGradle'
        publicDownloadNumbers = true
        licenses = ['MIT']
    }

}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId group_id
            artifactId artifact_id
            version versionName
        }
    }
}

// ---------- 本地测试时用以下代码 ----------

// 声明发布maven库的task,用gradle执行即可发布到指定url
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri('./out/mavenlib/'))
        }
    }
}

afterEvaluate {
    jar {
        from tasks['compileBuildConfig'].outputs
    }

//    task buildConfigJar(type: Jar, dependsOn: tasks['compileBuildConfig']) << {
//        classifier = 'buildConfig'
//        from tasks['compileBuildConfig'].outputs
//    }
}
